plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_name
}

repositories {
    // Add NeoForged repository.
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }

    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

loom {
    silentMojangMappingsLicense()
    accessWidenerPath = file("src/main/resources/vulkanmod.accesswidener")
    neoForge {
        accessWidenerPath = file("src/main/resources/vulkanmod.accesswidener")
        accessTransformers = files("src/main/resources/META-INF/accesstransformer.cfg")
    }
}

project.ext.lwjglVersion = "3.3.3"
project.ext.jomlVersion = "1.10.4"
project.ext.winNatives = "natives-windows"
project.ext.linuxNatives = "natives-linux"
project.ext.macosNatives = "natives-macos"
project.ext.macosArmNatives = "natives-macos-arm64"

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.officialMojangMappings()
    neoForge "net.neoforged:neoforge:$project.neoforge_version"

    implementation("org.lwjgl:lwjgl-vulkan:$lwjglVersion")

	ext.includeNatives = { name ->
		implementation("$name:$lwjglVersion")
		implementation("$name:$lwjglVersion:$winNatives")
		runtimeOnly("$name:$lwjglVersion:$linuxNatives")
		runtimeOnly("$name:$lwjglVersion:$macosNatives")
		runtimeOnly("$name:$lwjglVersion:$macosArmNatives")
	}

	includeNatives("org.lwjgl:lwjgl-vma")
	includeNatives("org.lwjgl:lwjgl-shaderc")

	//MoltenVK
	implementation("org.lwjgl:lwjgl-vulkan:$lwjglVersion:$macosNatives")
	implementation("org.lwjgl:lwjgl-vulkan:$lwjglVersion:$macosArmNatives")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
